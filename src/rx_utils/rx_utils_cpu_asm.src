;-------------------------------------------------------------------------------
; @file CPUユーティリティコード
; @author
;
; RXファミリソフトウェアマニュアルより、
; 関数呼び出し側でR1-R5, R14,R15は退避・復帰処理を行うことになっている。
; (例外として、割り込みは対象外)
;-------------------------------------------------------------------------------
    .SECTION P,CODE
;-------------------------------------------------------------------------------
; void rx_util_nop(void)
;
; NOP命令を発効する。
; インライン展開されないので、呼び出し元のスタック退避・復帰処理が入る分、処理が重くなる。
; 意図した機械語にならないだろう。
;-------------------------------------------------------------------------------
    .GLB _rx_util_nop
_rx_util_nop:
    NOP
    RTS
;-------------------------------------------------------------------------------
; void rx_util_wait(void)
;
; WAIT命令を発効し、割り込み待ちになる。
; CPUは特権（スーパーバイザ）モードに切り替わるので、使用時には注意すること。
;-------------------------------------------------------------------------------
    .GLB _rx_util_wait
_rx_util_wait:
    WAIT
    RTS
;-------------------------------------------------------------------------------
; void rx_util_enable_interrupt(void);
;
; 割り込みを許可する。
;
; PSWレジスタのIビットを設定し、割り込みを許可する。
; 特権モード（スーパーバイザモード）でのみ使用できる。
;-------------------------------------------------------------------------------
    .GLB _rx_util_enable_interrupt
_rx_util_enable_interrupt:
    SETPSW I
    RTS
;-------------------------------------------------------------------------------
; void rx_util_disable_interrupt(void);
;
; 割り込みを禁止する。
;
; PSWレジスタのIビットをクリアし、割り込みを禁止する。
; 特権モード（スーパーバイザモード）でのみ使用できる。
;-------------------------------------------------------------------------------
    .GLB _rx_util_disable_interrupt
_rx_util_disable_interrupt:
    CLRPSW I
    RTS
;-------------------------------------------------------------------------------
; uint32_t rx_util_get_psw(void);
;
; PSWレジスタの値を得る。
;
; @return PSWレジスタの値
;-------------------------------------------------------------------------------
    .GLB _rx_util_get_psw
_rx_util_get_psw:
    MVFC    PSW, R1
    RTS

;-------------------------------------------------------------------------------
; void rx_util_set_psw(uint32_t psw);
;
; PSWレジスタの値を設定する。
;
; ハードウェアの仕様により、ユーザーモードの場合、U, Iビットへの書き込みは無視される。
; RXファミリソフトウェアマニュアルの記述を見る限り、C,Z,S,O,U,Iビットの変更にしか使用できない。
; また、PMビットはスタックからの復帰時にしか変更できない。
; 割り込みレベル(IPL)を変更する場合にはrx_util_set_ipl()を使用すること。
;
; @param psw PSWレジスタの値
;-------------------------------------------------------------------------------
    .GLB _rx_util_set_psw
_rx_util_set_psw:
    MVTC R1, PSW
    RTS
;-------------------------------------------------------------------------------
;void rx_util_set_ipl(uint8_t level)
;
; 割り込みレベルを設定する。
; スーパーバイザモードでのみ使用できる。
; 16以上の割り込みレベルを指定した場合は無視され、何もしない。
;
; @param level 割り込みレベル(0～15)
;
; Note: SETIPL命令の仕様により、SETIPｌにはコンパイル時に決定される数値しか渡す事ができない。
;       そのため、0～15の長い分岐で実装する。(switch-caseに相当)
;-------------------------------------------------------------------------------
    .GLB _rx_util_set_ipl
_rx_util_set_ipl:
    SUB   #0, R1     ; 0を減算して、PSWの演算結果ビットを更新する。
    BEQ   set_ipl0   ; 演算結果がゼロだったら、set_ipl0に飛ぶ。
    SUB   #1, R1     ; R1から1を減算してR1に格納する。（デクリメントする)
    BEQ   set_ipl1   ; 演算結果が0だったらset_ipl1に飛ぶ。
    SUB   #1, R1
    BEQ   set_ipl2
    SUB   #1, R1
    BEQ   set_ipl3
    SUB   #1, R1
    BEQ   set_ipl4
    SUB   #1, R1
    BEQ   set_ipl5
    SUB   #1, R1
    BEQ   set_ipl6
    SUB   #1, R1
    BEQ   set_ipl7
    SUB   #1, R1
    BEQ   set_ipl8
    SUB   #1, R1
    BEQ   set_ipl9
    SUB   #1, R1
    BEQ   set_ipl10
    SUB   #1, R1
    BEQ   set_ipl11
    SUB   #1, R1
    BEQ   set_ipl12
    SUB   #1, R1
    BEQ   set_ipl13
    SUB   #1, R1
    BEQ   set_ipl14
    SUB   #1, R1
    BEQ   set_ipl15
    RTS
set_ipl0:
    MVTIPL #0
    RTS
set_ipl1:
    MVTIPL #1
    RTS
set_ipl2:
    MVTIPL #2
    RTS
set_ipl3:
    MVTIPL #3
    RTS
set_ipl4:
    MVTIPL #4
    RTS
set_ipl5:
    MVTIPL #5
    RTS
set_ipl6:
    MVTIPL #6
    RTS
set_ipl7:
    MVTIPL #7
    RTS
set_ipl8:
    MVTIPL #8
    RTS
set_ipl9:
    MVTIPL #9
    RTS
set_ipl10:
    MVTIPL #10
    RTS
set_ipl11:
    MVTIPL #11
    RTS
set_ipl12:
    MVTIPL #12
    RTS
set_ipl13:
    MVTIPL #13
    RTS
set_ipl14:
    MVTIPL #14
    RTS
set_ipl15:
    MVTIPL #15
    RTS

    .END


